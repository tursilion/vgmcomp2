REM NOTE: THIS PROGRAM CONTAINS DEMO.OBJ AND TISNHI.OBJ MERGED IN
REM BY TML'S HMLOADER. IF YOU USE A DIFFERENT VERSION OR ADDITIONAL
REM ASM, YOUR CALL LOAD VALUES WILL BE DIFFERENT. THIS SOURCE IS
REM PROVIDED FOR EASY REFERENCE ONLY. IF YOU PASTE IT IN, MAKE
REM SURE TO DELETE LINE 3 AND KEEP THE ONE GENERATED BY HMLOADER

3 CALL LOAD(8192,190,172)

REM STOP MUSIC AND INIT VARIABLES

10 CALL LINK("STOPSN")
20 OLDA=0 :: REM ONLY USED TO SEE IF THE PLAYING STATE CHANGED

REM DISPLAY SCREEN

100 CALL LINK("CLEAR")
110 CALL LINK("PRINT",1,1,"1 - ANTARCTIC ADVENTURE")
120 CALL LINK("PRINT",9,1,"2 - CALIFORNIA GAMES")
130 CALL LINK("PRINT",17,1,"3 - DOUBLE DRAGON")
140 CALL LINK("PRINT",25,1,"4 - MOONWALKER")
150 CALL LINK("PRINT",33,1,"5 - ALF")
160 CALL LINK("PRINT",41,1,"0 - STOP MUSIC")

REM GET A SPRITE MOVING SO WE CAN SEE WE DIDN'T BREAK AUTOMOTION

170 CALL LINK("CHAR",1,"183C3C187E182442")
180 CALL LINK("SPRITE",1,1,2,100,100,0,10)

REM MAIN LOOP - CHECK IF MUSIC IS PLAYING

200 CALL LINK("ACTIVE",A)
210 IF OLDA=A THEN 300
220 OLDA=A
230 IF A THEN 250
240 CALL LINK("PRINT",70,1,"----")
245 GOTO 300
250 CALL LINK("PRINT",70,1,"PLAY")

REM CHECK THE KEYBOARD FOR NEW MUSIC TO PLAY (0-5)

300 CALL KEY(0,K,S)
310 IF S=0 THEN 200
320 IF K<48 THEN 200
330 IF K>53 THEN 200
340 K=K-49

REM ALWAYS STOP THE MUSIC, THEN IF THE KEY WASN'T 0,
REM PLAY THE NEW ONE

350 CALL LINK("STOPSN")
355 IF K<0 THEN 200
360 CALL LINK("SONGGO",K)

REM BACK TO LOOP

370 GOTO 200
